// --> Build Plugins
plugins {
	id 'java'
	id 'jacoco'
	alias(libs.plugins.org.springframework.boot)
	alias(libs.plugins.io.spring.dependency.management)
}

group = 'com.application.server'
version = '0.0.1'

// --> Plugin config for 'java'
java {
	sourceCompatibility = '21'
	// Apply a specific Java toolchain to ease working on different environments.
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

// --> Plugin config for 'jacoco'
jacoco {
	toolVersion = "0.8.11"
	reportsDirectory = layout.buildDirectory.dir('reports/jacoco')
}

test {
	// Report is always generated after tests run
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	// Tests are required to run before generating the report
	dependsOn test
}
// --> End

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

// --> Dependencies
dependencies {
	implementation(libs.spring.boot.starter.security)
	implementation(libs.spring.boot.starter.web)
	compileOnly(libs.lombok)
	developmentOnly(libs.spring.boot.dev.tools)
	annotationProcessor(libs.spring.boot.configuration.processor)
	annotationProcessor(libs.spring.boot.dev.tools)
	testImplementation(libs.spring.boot.starter.test)
	testImplementation(libs.spring.security.test)
}

tasks.named('test') {
	useJUnitPlatform()
}

// --> Custom Tasks
tasks.register('hello') {
	doLast {
		println('Hello!')
	}
}

tasks.register('greet') {
	doLast {
		println('Running Custom Tasks!!')
	}
	dependsOn('hello')
}